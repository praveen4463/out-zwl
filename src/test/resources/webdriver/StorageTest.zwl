# storage test
# https://developer.mozilla.org/en-US/docs/Web/API/Window/localStorage
# https://developer.mozilla.org/en-US/docs/Web/API/Window/sessionStorage
# Note: I'm not going to verify whether selenium's api was able to set/remove on browser itself
# (could be done with js),
# but just that api's work without exception and returns expected value after a set operation.
# Selenium tests all their api so we can be sure it works on browser.

# first session storage
openUrl("https://developer.mozilla.org/en-US/docs/Web/API/Window/sessionStorage")
# clear any zwl key from storage in case the test left uncleared values due to an error.
for s in ssGetKeySet() {
  if startsWith(s, "zwl") {
    ssRemoveItem(s)
  }
}
originalSize = ssSize()

ssSetItem("zwl-key", "ABzz34$#")
ssSetItem("zwl-lang", "en-default")
ssSetItem("zwl-hash", "A===5474")

# refresh the page to see it retains
refresh()
assertTrue(ssSize() == originalSize + 3)
keySet = ssGetKeySet()
assertTrue(containsElement(keySet, "zwl-key"))
assertTrue(containsElement(keySet, "zwl-lang"))
assertTrue(containsElement(keySet, "zwl-hash"))
assertTrue(ssGetItem("zwl-key") == "ABzz34$#")
assertTrue(ssGetItem("zwl-lang") == "en-default")
assertTrue(ssGetItem("zwl-hash") == "A===5474")

ssRemoveItem("zwl-lang")
assertTrue(ssSize() == originalSize + 2)
for s in ssGetKeySet() {
  if startsWith(s, "zwl") {
    ssRemoveItem(s)
  }
}
assertTrue(ssSize() == originalSize)

winId = getCurrentWinId()

# now local storage
openUrlNewWin("https://developer.mozilla.org/en-US/docs/Web/API/Window/localStorage")
# clear any zwl key from storage in case the test left uncleared values due to an error.
for s in lsGetKeySet() {
  if startsWith(s, "zwl") {
    lsRemoveItem(s)
  }
}
originalSize = lsSize()

lsSetItem("zwl-key", "ABzz34$#")
lsSetItem("zwl-lang", "en-default")
lsSetItem("zwl-hash", "A===5474")

close() # close this new window to see values retained even when we closed and reopened
switchWin(winId)
openUrlNewWin("https://developer.mozilla.org/en-US/docs/Web/API/Window/localStorage")

assertTrue(lsSize() == originalSize + 3)
keySet = lsGetKeySet()
assertTrue(containsElement(keySet, "zwl-key"))
assertTrue(containsElement(keySet, "zwl-lang"))
assertTrue(containsElement(keySet, "zwl-hash"))
assertTrue(lsGetItem("zwl-key") == "ABzz34$#")
assertTrue(lsGetItem("zwl-lang") == "en-default")
assertTrue(lsGetItem("zwl-hash") == "A===5474")

lsRemoveItem("zwl-lang")
assertTrue(lsSize() == originalSize + 2)
for s in lsGetKeySet() {
  if startsWith(s, "zwl") {
    lsRemoveItem(s)
  }
}
assertTrue(lsSize() == originalSize)